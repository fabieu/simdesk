buildscript {
    ext {
        vaadinVersion = "24.7.6"
        flywayVersion = "11.8.2"
    }
}

plugins {
    id "java"
    id "io.spring.dependency-management" version "1.1.7"
    id "org.springframework.boot" version "3.5.0"
    id 'com.vaadin' version "${vaadinVersion}"
    id "org.flywaydb.flyway" version "${flywayVersion}"
}

group = "de.sustineo.simdesk"

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(21)
        vendor = JvmVendorSpec.ADOPTIUM
    }
}

repositories {
    mavenCentral()
    maven { setUrl("https://maven.vaadin.com/vaadin-addons") }
}

dependencyManagement {
    imports {
        mavenBom "com.vaadin:vaadin-bom:$vaadinVersion"
    }
}

dependencies {
    // Spring Boot
    implementation "org.springframework.boot:spring-boot-starter-web"
    implementation "org.springframework.boot:spring-boot-starter-actuator"
    implementation "org.springframework.boot:spring-boot-starter-validation"
    implementation "org.springframework.boot:spring-boot-starter-jdbc"
    implementation "org.springframework.boot:spring-boot-starter-security"
    implementation "org.springframework.boot:spring-boot-starter-cache"
    implementation "org.springframework.security:spring-security-oauth2-client"

    // Vaadin
    implementation "com.vaadin:vaadin-spring-boot-starter"
    implementation "software.xdev:vaadin-maps-leaflet-flow:4.6.3"

    // Database
    implementation "org.mybatis.spring.boot:mybatis-spring-boot-starter:3.0.4"
    runtimeOnly "org.postgresql:postgresql:42.7.6"
    runtimeOnly "org.xerial:sqlite-jdbc:3.49.1.0"
    implementation "org.flywaydb:flyway-core:${flywayVersion}"
    implementation "org.flywaydb:flyway-database-postgresql:${flywayVersion}"

    // Logging
    implementation "io.logz.logback:logzio-logback-appender:2.4.0"

    // Apache Commons
    implementation "org.apache.commons:commons-lang3:3.17.0"
    implementation 'commons-codec:commons-codec:1.18.0'
    implementation 'commons-io:commons-io:2.19.0'

    // Misc
    implementation "com.discord4j:discord4j-core:3.2.8"
    implementation 'com.opencsv:opencsv:5.11'

    // OpenAPI documentation
    implementation 'org.springdoc:springdoc-openapi-starter-webmvc-ui:2.8.8'
    implementation 'org.webjars:webjars-locator-lite:1.1.0'

    // Development
    developmentOnly 'org.springframework.boot:spring-boot-devtools'
    testImplementation "org.springframework.boot:spring-boot-starter-test"
    testImplementation 'org.testcontainers:junit-jupiter:1.21.1'
    testImplementation 'org.testcontainers:postgresql:1.21.1'

    compileOnly "org.projectlombok:lombok:1.18.38"
    annotationProcessor "org.projectlombok:lombok:1.18.38"
}

tasks {
    test {
        useJUnitPlatform()
    }

    bootJar {
        archiveFileName.set("${project.name}.jar")
    }

    compileJava {
        options.deprecation = true
    }
}

springBoot {
    buildInfo()
}

configurations {
    developmentOnly
    runtimeClasspath {
        extendsFrom developmentOnly
    }
}