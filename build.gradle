buildscript {
    ext {
        vaadinVersion = "24.4.12"
    }
    configurations.classpath {
        exclude group: 'com.vaadin', module: 'hilla-engine-core'
    }
}

plugins {
    id "java"
    id "io.spring.dependency-management" version "1.1.6"
    id "org.springframework.boot" version "3.3.4"
    id 'com.vaadin' version "${vaadinVersion}"
}

group = "de.sustineo.simdesk"

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(21)
        vendor = JvmVendorSpec.ADOPTIUM
    }
}

repositories {
    mavenCentral()
    maven { setUrl("https://maven.vaadin.com/vaadin-addons") }
}

dependencyManagement {
    imports {
        mavenBom "com.vaadin:vaadin-bom:$vaadinVersion"
    }
}

dependencies {
    // Spring Boot
    implementation "org.springframework.boot:spring-boot-starter-web"
    implementation "org.springframework.boot:spring-boot-starter-actuator"
    implementation "org.springframework.boot:spring-boot-starter-validation"
    implementation "org.springframework.boot:spring-boot-starter-jdbc"
    implementation "org.springframework.boot:spring-boot-starter-security"
    implementation "org.springframework.boot:spring-boot-starter-cache"
    implementation "org.springframework.security:spring-security-oauth2-client"

    // Vaadin
    implementation("com.vaadin:vaadin-spring-boot-starter") {
        exclude group: "com.vaadin", module: "flow-react"
        exclude group: "com.vaadin", module: "hilla"
        exclude group: "com.vaadin", module: "hilla-dev"
    }

    // Database
    implementation "org.mybatis.spring.boot:mybatis-spring-boot-starter:3.0.3"
    runtimeOnly "org.postgresql:postgresql:42.7.4"
    implementation "org.flywaydb:flyway-core:10.18.0"
    implementation "org.flywaydb:flyway-database-postgresql:10.18.0"

    // Logging
    implementation "io.logz.logback:logzio-logback-appender:2.0.1"

    // Apache Commons
    implementation "org.apache.commons:commons-lang3:3.17.0"
    implementation 'commons-codec:commons-codec:1.17.1'
    implementation 'commons-io:commons-io:2.17.0'

    // Misc
    implementation "com.discord4j:discord4j-core:3.2.6"
    implementation 'com.opencsv:opencsv:5.9'

    // OpenAPI documentation
    implementation 'org.springdoc:springdoc-openapi-starter-webmvc-ui:2.6.0'
    implementation 'org.webjars:webjars-locator-lite:1.0.0'

    // Development
    developmentOnly 'org.springframework.boot:spring-boot-devtools'
    testImplementation "org.springframework.boot:spring-boot-starter-test"
    testImplementation 'org.testcontainers:junit-jupiter:1.20.1'
    testImplementation 'org.testcontainers:postgresql:1.20.1'

    compileOnly "org.projectlombok:lombok:1.18.34"
    annotationProcessor "org.projectlombok:lombok:1.18.34"
}

tasks {
    test {
        useJUnitPlatform()
    }

    bootJar {
        archiveFileName.set("${project.name}.jar")
    }
}

springBoot {
    buildInfo()
}

configurations {
    developmentOnly
    runtimeClasspath {
        extendsFrom developmentOnly
    }
}