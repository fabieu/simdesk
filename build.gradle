buildscript {
    ext {
        vaadinVersion = "24.7.0"
        flywayVersion = "11.4.0"
    }
    configurations.classpath {
        exclude group: 'com.vaadin', module: 'hilla-engine-core'
    }
}

plugins {
    id "java"
    id "io.spring.dependency-management" version "1.1.7"
    id "org.flywaydb.flyway" version "${flywayVersion}"
    id "org.springframework.boot" version "3.4.4"
    id 'com.vaadin' version "${vaadinVersion}"
}

group = "de.sustineo.simdesk"

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(21)
        vendor = JvmVendorSpec.ADOPTIUM
    }
}

repositories {
    mavenCentral()
    maven { setUrl("https://maven.vaadin.com/vaadin-addons") }
}

dependencyManagement {
    imports {
        mavenBom "com.vaadin:vaadin-bom:$vaadinVersion"
    }
}

dependencies {
    // Spring Boot
    implementation "org.springframework.boot:spring-boot-starter-web"
    implementation "org.springframework.boot:spring-boot-starter-actuator"
    implementation "org.springframework.boot:spring-boot-starter-validation"
    implementation "org.springframework.boot:spring-boot-starter-jdbc"
    implementation "org.springframework.boot:spring-boot-starter-security"
    implementation "org.springframework.boot:spring-boot-starter-cache"
    implementation "org.springframework.security:spring-security-oauth2-client"

    // Vaadin
    implementation("com.vaadin:vaadin-spring-boot-starter") {
        exclude group: "com.vaadin", module: "flow-react"
        exclude group: "com.vaadin", module: "hilla"
        exclude group: "com.vaadin", module: "hilla-dev"
    }
    implementation "software.xdev:vaadin-maps-leaflet-flow:4.6.0"

    // Database
    implementation "org.mybatis.spring.boot:mybatis-spring-boot-starter:3.0.4"
    runtimeOnly "org.postgresql:postgresql:42.7.5"
    runtimeOnly "org.xerial:sqlite-jdbc:3.49.1.0"
    implementation "org.flywaydb:flyway-core:${flywayVersion}"
    implementation "org.flywaydb:flyway-database-postgresql:${flywayVersion}"

    // Logging
    implementation "io.logz.logback:logzio-logback-appender:2.2.0"

    // Apache Commons
    implementation "org.apache.commons:commons-lang3:3.17.0"
    implementation 'commons-codec:commons-codec:1.18.0'
    implementation 'commons-io:commons-io:2.18.0'

    // Misc
    implementation "com.discord4j:discord4j-core:3.2.7"
    implementation 'com.opencsv:opencsv:5.10'

    // OpenAPI documentation
    implementation 'org.springdoc:springdoc-openapi-starter-webmvc-ui:2.8.5'
    implementation 'org.webjars:webjars-locator-lite:1.1.0'

    // Development
    developmentOnly 'org.springframework.boot:spring-boot-devtools'
    testImplementation "org.springframework.boot:spring-boot-starter-test"
    testImplementation 'org.testcontainers:junit-jupiter:1.20.6'
    testImplementation 'org.testcontainers:postgresql:1.20.6'

    compileOnly "org.projectlombok:lombok:1.18.36"
    annotationProcessor "org.projectlombok:lombok:1.18.36"
}

tasks {
    test {
        useJUnitPlatform()
    }

    bootJar {
        archiveFileName.set("${project.name}.jar")
    }
}

springBoot {
    buildInfo()
}

configurations {
    developmentOnly
    runtimeClasspath {
        extendsFrom developmentOnly
    }
}